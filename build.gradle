import org.springframework.boot.gradle.plugin.SpringBootPlugin

plugins {
    id 'java-library'
    id 'maven-publish'
    alias libs.plugins.org.springframework.boot

    alias libs.plugins.licence
    alias libs.plugins.versions
    alias libs.plugins.versions.update
}

version = libs.versions.hivemq.version.get()
group = 'com.example.hivemq.boot'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }

    withSourcesJar()
}

repositories {
    mavenCentral()
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

dependencies {
    implementation platform(SpringBootPlugin.BOM_COORDINATES)
    implementation libs.org.springframework.boot.starter
    implementation libs.org.springframework.boot.starter.validation
    implementation libs.com.fasterxml.jackson.dataformat.xml

    api libs.com.hivemq.community.edition.embedded

    annotationProcessor platform(SpringBootPlugin.BOM_COORDINATES)
    annotationProcessor libs.bundles.annotation.processors

    testImplementation libs.org.springframework.boot.starter.test
    testRuntimeOnly libs.junit.platform.launcher
}

tasks.named("jar") {
    manifest {
        attributes 'HiveMQ-Version': version
    }
    archiveClassifier = ''
}

springBoot {
    buildInfo {
        properties {
            additional = [
                    'hivemq-ce': version,
                    'spring-boot': libs.versions.spring.boot.version.get()
            ]
        }
    }
}

tasks.named('test') {
    useJUnitPlatform()
}

publishing {
    publications {
        starter(MavenPublication) {
            from components.java
        }
    }

    repositories {
        mavenLocal()
    }
}

license {
    header = file("HEADER")
    mapping("java", "SLASHSTAR_STYLE")
}

downloadLicenses {
    dependencyConfiguration = "runtimeClasspath"
}

// We disable this tasks for safety as we do not have a Spring Boot app,
// but we need to apply the Spring Boot plugin in order to have
// build info.
tasks.named("bootRun") {
    enabled = false
}
tasks.named("bootJar") {
    enabled = false
}
tasks.named("bootBuildImage") {
    enabled = false
}
tasks.named("resolveMainClassName") {
    enabled = false
}
